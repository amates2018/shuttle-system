apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

// query git for the SHA, Tag and commit count. Use these to automate versioning.
final def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
final
def gitCommitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools
    defaultConfig {
        applicationId "io.ugshuttle"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode gitCommitCount
        versionName gitSha
        archivesBaseName = gitSha
//        multiDexEnabled true

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""

        final def filesAuthorityValue = applicationId + ".shareprovider"
        buildConfigField "String", "FILES_AUTHORITY", "\"${filesAuthorityValue}\""
        manifestPlaceholders = [filesAuthority: filesAuthorityValue]
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            versionNameSuffix '-debug'
        }
        release {
            minifyEnabled true
            shrinkResources true
            versionNameSuffix '-beta'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError true
        check 'NewApi', 'InlinedApi'
        fatal 'NewApi', 'InlinedApi'
        enable 'UnusedResources'
        checkReleaseBuilds true
        textReport false
        textOutput 'stdout'
        htmlReport true
        xmlReport true
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
}

dependencies {
    //core
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:${versions.kotlin}"

    //support library
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.android.support:customtabs:${versions.supportLibrary}"
    implementation "com.android.support:design:${versions.supportLibrary}"
    implementation "com.android.support:palette-v7:${versions.supportLibrary}"
    implementation "com.android.support:support-v4:${versions.supportLibrary}"
    implementation "com.android.support:recyclerview-v7:${versions.supportLibrary}"
    implementation "com.android.support:cardview-v7:${versions.supportLibrary}"
    implementation "com.android.support:support-dynamic-animation:${versions.supportLibrary}"
    implementation "com.android.support:appcompat-v7:${versions.supportLibrary}"
    implementation "com.android.support:support-core-utils:${versions.supportLibrary}"

    //glide image loading and caching
    implementation "com.github.bumptech.glide:glide:${versions.glide}"
    implementation 'com.android.support:appcompat-v7:27.1.1'
    annotationProcessor "com.github.bumptech.glide:compiler:${versions.glide}"
    kapt "com.github.bumptech.glide:compiler:${versions.glide}"
    implementation "com.github.bumptech.glide:okhttp3-integration:${versions.glide}"
    implementation("com.github.bumptech.glide:recyclerview-integration:${versions.glide}") {
        transitive = false
    }

    //server api
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation "com.squareup.retrofit2:retrofit:${versions.retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${versions.retrofit}"
    implementation "com.squareup.retrofit2:converter-scalars:${versions.retrofit}"
    implementation "com.squareup.okhttp3:okhttp:${versions.okhttp}"
    implementation "com.squareup.okhttp3:logging-interceptor:${versions.okhttp}"

    //multidex
    implementation 'com.android.support:multidex:1.0.3'

    //firebase
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true
    }
    implementation "com.google.firebase:firebase-core:${versions.google}"
    implementation "com.google.firebase:firebase-auth:${versions.google}"
    implementation "com.google.firebase:firebase-firestore:${versions.google}"
    implementation "com.google.firebase:firebase-messaging:${versions.google}"
    implementation "com.google.firebase:firebase-storage:${versions.google}"

    //google
    implementation "com.google.android.gms:play-services-maps:${versions.google}"
    implementation "com.google.android.gms:play-services-location:${versions.google}"
    implementation "com.google.android.gms:play-services-places:${versions.google}"

    //misc
    implementation "com.afollestad.material-dialogs:core:${versions.material_dialogs}"
    implementation "com.miguelcatalan:materialsearchview:${versions.materialsearchview}"

    //butterknife
    implementation "com.jakewharton:butterknife:${versions.butterknife}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${versions.butterknife}"
    kapt "com.jakewharton:butterknife-compiler:${versions.butterknife}"

    //library for animations and other utilities
    implementation project(":dev")

    //pubnub
    implementation group: 'com.pubnub', name: 'pubnub-gson', version: '4.12.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.2'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.9.2'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.2'
    implementation group: 'com.google.guava', name: 'guava', version: '23.2-android'
}
apply plugin: 'com.google.gms.google-services'
